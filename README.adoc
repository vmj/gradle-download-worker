image:https://travis-ci.org/vmj/gradle-download-worker.svg?branch=master["Build Status", link="https://travis-ci.org/vmj/gradle-download-worker"]

= A parallel download task type for Gradle

This Gradle plugin implements a parallel download task type.

The releases are available via
https://search.maven.org/artifact/fi.linuxbox.gradle/gradle-download/0.5/jar[Maven Central],
https://bintray.com/bintray/jcenter/fi.linuxbox.gradle%3Agradle-download[Bintray], and
https://plugins.gradle.org/plugin/fi.linuxbox.download[Gradle Plugin Portal].

This plugin requires *Gradle 5.0 or newer*.

[NOTE]
====
Version 0.5 has a *breaking change* in that it drops the word `worker` from many places.

[source,groovy]
----
plugins {
    id 'fi.linuxbox.download' version '0.5' // <1>
}

import fi.linuxbox.gradle.download.Download // <2> <3>

task('foo', type: Download) { // <3>
    //...
}
----

<1> the plugin ID changed from `fi.linuxbox.download.worker` to `fi.linuxbox.download`
<2> the implementation package changed from `fi.linuxbox.gradle.download.worker` to `fi.linuxbox.gradle.download`
<3> the task class name changed from `DownloadWorkerTask` to just `Download`

In addition, the plugin class name changed from `DownloadWorkerPlugin` to `DownloadPlugin`.
This is relevant if you are applying the plugin via
https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/PluginContainer.html#apply-java.lang.Class-[PluginContainer.apply(Class)]
method.

In case you are using this project as a library dependency, the artifact ID changed from `gradle-download-worker` to
`gradle-download`. In Gradle, that would look like:

[source,groovy]
----
dependencies {
    compile 'fi.linuxbox.gradle:gradle-download:0.5'
}
----

And a Maven equivalent is:

[source,xml]
----
<dependency>
  <groupId>fi.linuxbox.gradle</groupId>
  <artifactId>gradle-download</artifactId>
  <version>0.5</version>
</dependency>
----

====


== Usage

Basic usage is as follows:

[source,groovy]
----
plugins {
    id 'fi.linuxbox.download' version '0.5'
}

import fi.linuxbox.gradle.download.Download

task("my-download", type: Download) {
    from 'https://www.google.com/robots.txt'
    to "$buildDir/robots.txt"
}
----

A more interesting example:

[source,groovy]
----
plugins {
    id 'fi.linuxbox.download' version '0.5'
}

import fi.linuxbox.gradle.download.Download

task downloadAll {
    group 'My tasks'
    description 'Download all ChangeLogs'
}

ext {
    mirror = 'http://ftp.osuosl.org/pub/slackware'
    distroNames = ['slackware', 'slackware64']
    distroVersions = ['13.0', '13.1', '13.37', '14.0', '14.1', '14.2']
}

distroNames.each { final distroName ->
    distroVersions.each { final distroVersion ->
        final distro = "$distroName-$distroVersion"
        final path = "$distroName/$distroVersion"

        // Define a parallel download task for this distro version
        final download = task("download-$distro-changelog", type: Download) {
            from "$mirror/$distro/ChangeLog.txt"
            to project.layout.buildDirectory.file("changelogs/$path/ChangeLog.txt")
        }

        // Just to demo the UP-TO-DATE functionality:
        // even though the download task does some work (conditional GET)
        // it doesn't necessarily touch the artifact.
        // That allows Gradle to skip the copy task.
        final copy = task("copy-$distro-changelog", type: Copy) {
            from download
            into project.layout.buildDirectory.dir("copies/$path/")
        }

        downloadAll.dependsOn copy
    }
}
----

With above build script, the first run of `gradle downloadAll` would download
the ChangeLog files in parallel.  Then it would copy each ChangeLog as
soon as it was downloaded, i.e. in parallel.

The second invocation of `gradle downloadAll` would finish pretty quickly,
as the download task will make a conditional HTTP GET and,
since the ChangeLogs will not be updated,
copy tasks will report UP-TO-DATE.

== Task Configuration

This download task can be configured with the following properties:

.Configuration properties
[cols="2,2,6"]
|===
|Property | Default | Description

|`from` | -
| The HTTP or HTTPS URL from which to download.  This is mandatory

|`to` | -
| Destination file.  This is mandatory, and can be anything that is understood by the
https://docs.gradle.org/current/dsl/org.gradle.api.Project.html#org.gradle.api.Project:file(java.lang.Object)[Project.file(path)]
method.

|`connectTimeout` | 30000 (30 seconds)
|Milliseconds before timing out the connection attempt. Set to zero to disable the timeout (infinite timeout).

|`readTimeout` | 30000 (30 seconds)
|Milliseconds before timing out the socket reading. Set to zero to disable the timeout (infinite timeout).

|===

