plugins {
    id 'com.gradle.build-scan' version '1.16'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'org.unbroken-dome.test-sets' version '1.5.1'
    id "com.bmuschko.nexus" version "2.3.1"
}

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group 'fi.linuxbox.gradle'
version '0.6-SNAPSHOT'
ext {
    pluginId = "fi.linuxbox.download"
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

modifyPom {
    project {
        name 'gradle-download'
        description 'This plugin provides a Download task type for Gradle.  Tasks of this type can do HTTP and HTTPS downloads in parallel.'
        url 'https://github.com/vmj/gradle-download-worker'

        licenses {
            license {
                name 'GNU General Public License, version 3'
                url 'http://www.gnu.org/licenses/gpl-3.0.html'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'vmj'
                name 'Mikko VÃ¤rri'
                email 'mikko@varri.fi'
            }
        }

        scm {
            url 'https://github.com/vmj/gradle-download-worker'
            connection 'scm:https://github.com/vmj/gradle-download-worker.git'
            developerConnection 'scm:git@github.com:vmj/gradle-download-worker.git'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

testSets {
    functionalTest
}
check.dependsOn functionalTest

functionalTest {
    if(gradle.startParameter.isOffline()) {
        systemProperties.IS_OFFLINE = '1'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'

    functionalTestCompile("org.spockframework:spock-core:1.1-groovy-${spockGroovyVer}") {
        exclude module: 'groovy-all'
    }
}

// configure java-gradle-plugin plugin
gradlePlugin {
    testSourceSets sourceSets.functionalTest

    plugins {
        "DownloadPlugin" {
            id = pluginId
            implementationClass = "fi.linuxbox.gradle.download.DownloadPlugin"
        }
    }
}

// configure plugin-publish plugin
pluginBundle {
    website = 'https://github.com/vmj/gradle-download-worker'
    vcsUrl = 'https://github.com/vmj/gradle-download-worker'

    plugins {
        "DownloadPlugin" {
            id = pluginId
            displayName = "Parallel Download Task Type for Gradle"
            description = "This plugin provides a Download task type for Gradle.  Tasks of this type can do HTTP and HTTPS downloads in parallel."
            tags = ['download', 'task', 'url', 'http', 'https']
        }
    }
}
