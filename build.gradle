plugins {
    id 'com.gradle.build-scan' version '3.1'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'maven-publish'
}

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group 'fi.linuxbox.gradle'
version '0.7-SNAPSHOT'
ext {
    pluginId = "fi.linuxbox.download"
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

final sourceJar = tasks.register("publishPluginSourcesJar", Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
    // classifier changes to archiveClassifier at some point after 5.0
    //archiveClassifier = 'sources'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

test {
    if(gradle.startParameter.isOffline()) {
        systemProperties.IS_OFFLINE = '1'
    }
}

dependencies {
    testImplementation("org.spockframework:spock-core:1.3-groovy-${spockGroovyVer}")
}

// configure java-gradle-plugin plugin
gradlePlugin {
    plugins {
        "DownloadPlugin" {
            id = pluginId
            implementationClass = "fi.linuxbox.gradle.download.DownloadPlugin"
        }
    }
}

// configure plugin-publish plugin
pluginBundle {
    website = 'https://github.com/vmj/gradle-download-worker'
    vcsUrl = 'https://github.com/vmj/gradle-download-worker'

    plugins {
        "DownloadPlugin" {
            id = pluginId
            displayName = "Parallel Download Task Type for Gradle"
            description = "This plugin provides a Download task type for Gradle.  Tasks of this type can do HTTP and HTTPS downloads in parallel."
            tags = ['download', 'task', 'url', 'http', 'https']
        }
    }
}

// configure maven-publish
publishing {
    publications {
        // name chosen by plugin-publish plugin
        pluginMaven(MavenPublication) {
            // already added by plugin-publish plugin
            //from components.java
            afterEvaluate {
                artifact project.tasks.findByPath(':publishPluginGroovyDocsJar')
                artifact sourceJar.get() // FIXME
            }
            pom {
                name = 'gradle-download'
                description = 'This plugin provides a Download task type for Gradle.  Tasks of this type can do HTTP and HTTPS downloads in parallel.'
                url = 'https://github.com/vmj/gradle-download-worker'

                licenses {
                    license {
                        name = 'GNU General Public License, version 3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'vmj'
                        name = 'Mikko VÃ¤rri'
                        email = 'mikko@varri.fi'
                    }
                }

                scm {
                    url = 'https://github.com/vmj/gradle-download-worker'
                    connection = 'scm:https://github.com/vmj/gradle-download-worker.git'
                    developerConnection = 'scm:git@github.com:vmj/gradle-download-worker.git'
                }
            }
        }
    }
    repositories {
        mavenCentral()
    }
}
